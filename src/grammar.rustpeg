use super::ast::{HexNumber, DecNumber, HexLiteral, StringLiteral, LinkerSymbol,
                 Identifier, LabelDefinition, Assignment, IdentifierList};

pub identifier_or_list -> IdentifierList
  = __ i:identifier { IdentifierList::new(i, vec![]) }
  / __ "(" is:identifier_list ")" { is }

pub identifier_list -> IdentifierList
  = __ i:identifier is:(identifier_list_clause*) { IdentifierList::new(i, is) }

identifier_list_clause -> Identifier
  = __ "," __ i:identifier { i }

pub assignment -> Assignment
  = "=:" i:identifier  { Assignment::new(i) }

pub label_definition -> LabelDefinition
  = __ i:identifier ':' { LabelDefinition::new(i) }

pub identifier -> Identifier
  = __ name:$([a-zA-Z_$] [a-zA-Z_0-9]*) { Identifier::new(name) }

pub linker_symbol -> LinkerSymbol
  = __ "linkerSymbol" "(" s:string ")" { LinkerSymbol::new(s) }

pub hex_literal -> HexLiteral
  = __ "hex" b:bytestr { HexLiteral::new(b.as_str()) }

bytestr -> String
  = "\"" b:bytes "\"" { b }
  / "\'" b:bytes "\'" { b }
bytes -> String
  = b:$(([0-9A-Fa-f]{2})*) { b.to_string() }

pub string -> StringLiteral
  = __ string:(doubleQuotedString) { StringLiteral::new(string) }

pub hex_number -> HexNumber
  = __ "0x" uint:$([0-9A-Fa-f]+) { HexNumber::new(uint) }

pub dec_number -> DecNumber
  = __ uint:$([0-9]+) { DecNumber::new(uint) }

doubleQuotedString -> String
  = '"' s:doubleQuotedCharacter* '"' { s.into_iter().collect() }

doubleQuotedCharacter -> char
  = simpleDoubleQuotedCharacter

simpleDoubleQuotedCharacter -> char
  = (!('"' / "\r" / "\n" / "\\") / "\\" .) c:$. { c.chars().next().unwrap() }

__ = (whitespace / comment)*

whitespace = [ \t\n\r]
comment
  = "//" (!'\n' .)*
  / "/*" (!"*/" .)* "*/"
